#Использовать "../../core"

Перем Лог;

Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Опция("q quiet", Ложь, "вывод только имени хранилища");
	Команда.Опция("s search-name", "", "Имя пользователя для поиска");
	Команда.Аргумент("WORKDIR", "", "Каталог исходников внутри локальной копии git-репозитария.")
					.ТСтрока()
					.ВОкружении("GITSYNC_WORKDIR")
					.Обязательный(Ложь)
					.ПоУмолчанию(ТекущийКаталог());

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	Лог = ПараметрыПриложения.Лог();

	ВыводТолькоНазваний = Команда.ЗначениеОпции("q");

	ИмяДляПоиска = Команда.ЗначениеОпции("search-name");

	КаталогРабочейКопии		= Команда.ЗначениеАргумента("WORKDIR");
	ФайлКаталогРабочейКопии = Новый Файл(КаталогРабочейКопии);
	КаталогРабочейКопии = ФайлКаталогРабочейКопии.ПолноеИмя;

	Лог.Отладка("КаталогРабочейКопии = " + КаталогРабочейКопии);

	МассивФайлов = НайтиФайлы(КаталогРабочейКопии, "src");
	КаталогИсходников = КаталогРабочейКопии;
	Если МассивФайлов.Количество() > 0  Тогда
		КаталогИсходников = МассивФайлов[0].ПолноеИмя;
	КонецЕсли;

	ОбщиеПараметры = ПараметрыПриложения.Параметры();
	МенеджерПлагинов = ОбщиеПараметры.УправлениеПлагинами;
	
	ИндексПлагинов = МенеджерПлагинов.ПолучитьИндексПлагинов();

	Распаковщик = Новый МенеджерСинхронизации();
	Распаковщик.ВерсияПлатформы(ОбщиеПараметры.ВерсияПлатформы)
			   .ДоменПочтыПоУмолчанию(ОбщиеПараметры.ДоменПочты)
			   .ИсполняемыйФайлГит(ОбщиеПараметры.ПутьКГит)
			   .ПодпискиНаСобытия(ИндексПлагинов)
			   .ПараметрыПодписокНаСобытия(Команда.ПараметрыКоманды())
			   .УровеньЛога(ПараметрыПриложения.УровеньЛога());
	
	ПутьКФайлуAUTHORS = ОбъединитьПути(КаталогИсходников, Распаковщик.ИмяФайлаАвторов());

	БудемИскать = Ложь;
	Если Не ПустаяСтрока(ИмяДляПоиска) Тогда
		РегуляркаДляПоиска =  Новый РегулярноеВыражение(ИмяДляПоиска);
		БудемИскать = Истина;
	КонецЕсли;

	ТаблицаПользователей = Распаковщик.ПрочитатьФайлАвторовГитВТаблицуПользователей(ПутьКФайлуAUTHORS);

	Если НЕ ВыводТолькоНазваний Тогда
		Сообщить("Список синхронизированных авторов:");
		Сообщить("Всего авторов:" + Строка(ТаблицаПользователей.Количество()));
	КонецЕсли;

	Для каждого Пользователь Из ТаблицаПользователей Цикл
		
		СтрокаВывода = СтрШаблон("%1=%2", Пользователь.Автор, Пользователь.ПредставлениеАвтора);

		Если БудемИскать Тогда
			
			КоллекцияСовпадений = РегуляркаДляПоиска.НайтиСовпадения(СтрокаВывода);

			Если КоллекцияСовпадений.Количество() = 0  Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;

		Если ВыводТолькоНазваний Тогда
			Сообщить(Пользователь.Автор);
		Иначе
			Сообщить(СтрокаВывода);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры